# shellcheck shell=bash
[ -r "$HOME/.shellrc" ] && source "$HOME/.shellrc"

# Zsh Options
setopt autocd             # Enter directory just by typing its name
# Disable automatic command correction
# setopt correct
setopt nocaseglob         # Case insensitive globbing
setopt extended_glob      # Extended globbing
setopt hist_ignore_all_dups  # Donâ€™t store duplicate history
setopt share_history         # Share command history between sessions
setopt inc_append_history     # Append to history file immediately
setopt hist_ignore_space      # Ignore commands starting with spaces
setopt globdots               # Include dotfiles in globbing

# History
HISTSIZE=50000
SAVEHIST=50000
HISTFILE=~/.zsh_history

# Key Bindings
# bindkey -v                # Use vim keybindings
# KEYTIMEOUT=1              # Reduce delay for ESC
# bindkey -M vicmd '_' vi-beginning-of-line  # '_' moves to start of line in normal mode

bindkey -e                # Use emacs keybindings
bindkey '^[[1;5C' forward-word     # Ctrl+Right
bindkey '^[[1;5D' backward-word    # Ctrl+Left

# Zsh Plugins: autosuggestions & syntax-highlighting
# Load from common installation paths without relying on brew
for plugin in zsh-autosuggestions zsh-syntax-highlighting; do
  for dir in "/usr/local/share/$plugin" "/opt/homebrew/share/$plugin" "/usr/share/$plugin"; do
    if [[ -r "$dir/$plugin.zsh" ]]; then
      source "$dir/$plugin.zsh"
      break
    fi
  done
done

# Autocomplete
autoload -Uz compinit && compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'

# FZF history search widget
if (( $+commands[fzf] )); then
  function _fzf_history_widget() {
    local selected
    selected=$(
      fc -nrl 1 |
      awk '!seen[$0]++' |
      fzf  --height=80% \
           --reverse \
           --tiebreak=index \
           --no-sort \
           --prompt='History> ' \
           --style=minimal \
           --query="$LBUFFER"
    ) || return

    LBUFFER=$selected
    zle redisplay
  }

  zle -N _fzf_history_widget
  bindkey '^R' _fzf_history_widget
fi

# Load machine-specific overrides, if present
# shellcheck disable=SC1090
[[ -r "$HOME/.zshrc.local" ]] && source "$HOME/.zshrc.local"
